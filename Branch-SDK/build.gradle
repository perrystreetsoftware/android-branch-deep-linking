apply plugin: 'com.android.library'

//File name for out put jar created
def jarFileName

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation "com.android.support:support-annotations:$supportLibraryVersion"
    implementation 'com.crashlytics.sdk.android:answers-shim:0.0.6'

    // This is an optional dependency
    // Please note that the Branch SDK does not require Firebase to operate. This dependency is listed here so there will not be build errors,
    // but the library is *not* added to your app unless you do so yourself. Please check the code in gradle-mvn-push script to see how this works
    implementation "com.google.firebase:firebase-appindexing:$playServicesVersion"
    implementation 'com.android.installreferrer:installreferrer:1.0'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    publishNonDefault true

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName '2.12.0'
        versionCode 1

        buildConfigField "String", "DEVICE_LOCALHOST", rootProject.ext.devHostDevice
        buildConfigField "String", "PRODUCTION_BASE_URL", rootProject.ext.prodBaseUrl
        buildConfigField "String", "BASE_PATH", rootProject.ext.branchPath
    }

    // Using gradle plugin 3 you need to specify flavor dimensions.
    flavorDimensions 'buildEnv'

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        dev {
            dimension 'buildEnv'
        }
        prod {
            dimension 'buildEnv'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['resources']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        //instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    lintOptions {
        abortOnError false
    }
}

//------------- Jar Generation code. ---------------//
//Run makeJar to create a jar file for SDK to the route directory
task clearJar(type: Delete) {
    delete 'build/libs/'+ jarFileName
}

task makeJar(type: Copy) {
    from('build/intermediates/intermediate-jars/release/')
    into('../')
    include('classes.jar')
    rename ('classes.jar', 'Branch-'+project.VERSION_NAME+'.jar')
}

makeJar.dependsOn(clearJar, build)

apply from: 'https://raw.githubusercontent.com/BranchMetrics/GradleToMavenOptionalDependencies/master/gradle-maven-push.gradle'
